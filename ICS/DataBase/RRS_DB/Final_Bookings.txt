CREATE OR ALTER PROCEDURE sp_searchtrains
    @sourcestationid INT,
    @destinationstationid INT,
    @journeydate DATE,
    @seattype NVARCHAR(10),
    @passengercount INT
AS
BEGIN
    SET NOCOUNT ON;

    -- Calculate correct day index (1-based, Monday=1...Sunday=7)
    DECLARE @dayindex INT = ((DATEPART(weekday, @journeydate) + @@DATEFIRST - 2) % 7) + 1;

    SELECT 
        t.train_id,
        t.train_name,
        ts.dep_time AS departure_time,
        td.arr_time AS arrival_time,
        ISNULL(s.available_seats, 0) AS available_seats,
        ISNULL(s.fare_per_passenger, 0) AS fare_per_passenger,
        CASE 
            WHEN ISNULL(s.available_seats, 0) >= @passengercount THEN 'available'
            ELSE 'waitlist'
        END AS booking_status
    FROM trains t
    INNER JOIN train_stations ts ON ts.train_id = t.train_id AND ts.station_id = @sourcestationid
    INNER JOIN train_stations td ON td.train_id = t.train_id AND td.station_id = @destinationstationid
    LEFT JOIN seat_availability s ON s.train_id = t.train_id AND s.journey_date = @journeydate AND s.seat_type = @seattype
    WHERE 
        t.is_active = 1
        AND ts.station_seq < td.station_seq
        -- Ensure the train runs on the journey date!
        AND LEN(t.running_days) >= @dayindex
        AND SUBSTRING(t.running_days, @dayindex, 1) = '1'
END